zooArrays_str;
zooCSTUtils;
zooFlags;
zooUtils;
zooVectors;


//------
// -dampen [float]				this flag allows control over the dampen range - lower values cause the dampening to happen over a shorter range.  setting to zero turns damping off.  its technical range is 0-100, but in practice, its probably best to keep between 0-25.  defaults to 12
// -dampstrength [float]		this is the strength of the dampening - ie how much the arm gets elongated as it nears maximum extension.  the technical range is 0-100, but in reality, you'd probably never go beyond 10.  default is 2
//------
global proc string[] zooCSTMakeStretchy( string $control, string $ikHandle, string $optionStr ) {
	//------
	//parse optionStr to determine which flags are enabled for this proc
	//------
		string $optionTemp[] = {};
		string $axis = "";
		string $primitive = "";
		string $startObj = "";
		string $endObj = "";
		string $prefix = "";
		string $parts = "";
		float $scale = 1;
		float $dampRange = 12.0;
		float $dampStrength = 2.0;
		int $register = 0;
		int $invert = 1;
		int $elbowpos = 1;

		$optionTemp = `zooGetFlagArguments $optionStr axis`; if( `size $optionTemp` ) $axis = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr register`; if( `size $optionTemp` ) $register = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr primitive`; if( `size $optionTemp` ) $primitive = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr startObj`; if( `size $optionTemp` ) $startObj = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr endObj`; if( `size $optionTemp` ) $endObj = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr prefix`; if( `size $optionTemp` ) $prefix = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr scale`; if( `size $optionTemp` ) $scale = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr invert`; if( $optionTemp[0] == "1" ) $invert = -1;
		$optionTemp = `zooGetFlagArguments $optionStr elbowpos`; if( `size $optionTemp` ) $elbowpos = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr parts`; if( `size $optionTemp` ) $parts = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr dampen`; if( `size $optionTemp` ) $dampRange = $optionTemp[0];
		$optionTemp = `zooGetFlagArguments $optionStr dampstrength`; if( `size $optionTemp` ) $dampStrength = $optionTemp[0];
		if( $dampRange <= 0 ) $dampstrength = 0;
		$dampRange = abs($dampRange);
		$dampStrength = abs($dampStrength);
		$dampRange /= 200.0;  //divide by 200 because we want the input range to be 0-100, but internally it needs to range from 0-0.5
		$dampStrength /= 100.0;


	//------
	//setup some current unit variables, and take parity into account
	//------
		string $stretchAuto = "autoStretch";
		string $stretchName = "stretch";
		int $parityFactor = 1;

		$parityFactor *= $invert;
		addAttr -ln $stretchAuto -at double -min 0 -max 1 -dv 1 $control;
		addAttr -ln $stretchName -at double -min 0 -max 10 -dv 0 $control;
		setAttr -k 1 ( $control +"."+ $stretchAuto );
		setAttr -k 1 ( $control +"."+ $stretchName );


	//------
	//build the network for distributing stretch from the fk controls to the actual joints
	//------
		string $plusNodes[] = {};
		string $initialNodes[] = {};
		string $fractionNodes[] = {};
		string $clients[] = {};
		string $allNodes[] = {};

		$clients = `ikHandle -q -jl $ikHandle`;
		if( $axis == "" ) $axis = `zooCSTJointDirection $clients[1]`;  //if no axis is specified, assume the second joint in the chain has the correct axis set
		string $cons = connectionInfo("-sfd",(`ikHandle -q -ee $ikHandle`+".t"+$axis));
		$clients[( `size $clients` )] = `match "^[^.]+" $cons`;

		for( $n=0; $n<`size $clients`; $n++ ) $fractionNodes[( `size $fractionNodes` )] = `shadingNode -asUtility -name ( $clients[$n] +"_fraction_pos" ) multiplyDivide`;
		if( !`objExists $startObj` ) $startObj = $clients[0];
		if( !`objExists $endObj` ) $endObj = $ikHandle;


	//------
	//setup the minimum limits for each joint in the chain
	//------
		string $clientAxes[] = {};  //not really used
		float $clientLengths[] = {};
		float $totalLength = 0;

		$clientAxes[0] = `zooCSTJointDirection $clients[0]`;
		$clientLengths[0] = `getAttr ( $clients[0] +".t"+ $axis )`;
		for( $n=1; $n<`size $clients`; $n++ ) {
			string $clientAxis = `zooCSTJointDirection $clients[$n]`;
			float $length = `getAttr ( $clients[$n] +".t"+ $axis )`;
			float $min = 0; int $minOn = 1;
			float $max = 0; int $maxOn = 0;

			$clientAxes[$n] = $clientAxis;
			$clientLengths[$n] = $length;
			$totalLength += abs($clientLengths[$n]);
			}


	//------
	//build the network to measure limb length
	//------
		string $loc_a = `group -em`;
		string $loc_b = `group -em`;
		string $measure = $loc_b;
		string $aimAxis = "1 0 0";
		string $aimName = "tx";

		parent $loc_b $loc_a;
		string $constraint_a = zooGetElement_str(0,`pointConstraint $startObj $loc_a`);
		$aimAxis = `zooCSTUpVector $loc_a (zooBetweenVector($loc_a,$loc_b))`;
		$aimName = "t"+ `zooCSTUpVectorAsName $loc_a (zooBetweenVector($loc_a,$loc_b))`;
		$aimAxis = `substitute "-" $aimAxis ""`;  //remove the negative sign - we always want the child to be on a positive axis

		string $aimConstraint = zooGetElement_str(0,eval( "aimConstraint -aimVector "+ $aimAxis +" "+ $endObj +" "+ $loc_a ));
		setAttr ( $loc_b +"."+ $aimName ) $totalLength;
		makeIdentity -a 1 -t 1 $loc_b;  //by doing this, the zero point for the null is the max extension for the limb
		string $constraint_b = zooGetElement_str(0,`pointConstraint $endObj $loc_b`);
		$allNodes = `zooAddArray_str $allNodes { $loc_a, $loc_b, $constraint_a, $constraint_b, $aimConstraint }`;
		zooAttrState "-attrs t r s -k 0 -l 1" $loc_a;
		zooAttrState "-attrs t r s -k 0 -l 1" $loc_b;


	//------
	//create the stretch network
	//------
		string $stretchEnable = `shadingNode -asUtility -n( $prefix +"_stretch_enable" ) multiplyDivide`;  //blends the auto length smooth back to zero when blending to fk
		string $fkikBlend = `shadingNode -asUtility -n( $prefix +"_fkik_stretch_blend" ) multiplyDivide`;  //blends the auto length smooth back to zero when blending to fk
		string $actualLength = `shadingNode -asUtility -n( $prefix +"_actual_length#" ) plusMinusAverage`;  //adds the length mods to the normal limb length
		string $lengthMods = `shadingNode -asUtility -n( $prefix +"_length_mods#" ) plusMinusAverage`;  //adds all lengths together
		string $finalLength = `shadingNode -asUtility -n( $prefix +"_final_length#" ) clamp`;  //clamps the length the limb can be
		string $manualStretchMult = `shadingNode -asUtility -n( $prefix +"_manualStretch_range_multiplier" ) multiplyDivide`;  //multiplys manual stretch to a sensible range
		string $dampen = `createNode -n( $prefix +"_dampen" ) animCurveUU`;

		for( $n=1; $n<`size $clientLengths`; $n++ )   //if any of the lengths are negative, the stretch will still be wrong, but will be easier to fix manually if this number is correct
		setKeyframe -f($totalLength*-0.5) -v 0 $dampen;
		setKeyframe -f($totalLength*-1*$dampRange) -v( $totalLength*$dampStrength) $dampen;
		setKeyframe -f($totalLength*$dampRange) -v 0 $dampen;
		keyTangent -f ":" -itt flat -ott flat $dampen;

		//NOTE: the second term attribute of the length condition node holds the initial length for the limb, and is thus connected to the false attribute of all condition nodes
		setAttr ( $manualStretchMult +".input2X" ) ( $totalLength/10 );
		setAttr ( $actualLength +".input1D[0]" ) $totalLength;
		setAttr ( $actualLength +".input1D[0]" ) $totalLength;
		setAttr ( $finalLength +".minR" ) $totalLength;
		setAttr ( $finalLength +".maxR" ) ($totalLength*3);
		connectAttr -f ( $measure +"."+ $aimName ) ( $dampen +".input" );
		connectAttr -f ( $lengthMods +".output1D" ) ( $fkikBlend +".input1X" );
		connectAttr -f ( $ikHandle +".ikBlend" ) ( $fkikBlend +".input2X" );
		connectAttr -f ( $fkikBlend +".outputX" ) ( $stretchEnable +".input1X" );
		connectAttr -f ( $control +"."+ $stretchAuto ) ( $stretchEnable +".input2X" );
		connectAttr -f ( $measure +"."+ $aimName ) ( $lengthMods +".input1D[0]" );
		connectAttr -f ( $control +"."+ $stretchName ) ( $manualStretchMult +".input1X" );
		connectAttr -f ( $manualStretchMult +".outputX" ) ( $lengthMods +".input1D[1]" );
		connectAttr -f ( $dampen +".output" ) ( $lengthMods +".input1D[2]" );
		connectAttr -f ( $stretchEnable +".outputX" ) ( $actualLength +".input1D[1]" );
		connectAttr -f ( $actualLength +".output1D" ) ( $finalLength +".inputR" );
		$allNodes = `zooAddArray_str $allNodes { $dampen, $lengthMods, $actualLength, $finalLength, $manualStretchMult, $fkikBlend, $stretchEnable }`;


	//------
	//connect the stretch distribution network up - NOTE this loop starts at 1 because we don't need to connect the
	// start of the limb chain (ie the bicep or the thigh) as it doesn't move
	//------
		for( $n=1; $n<`size $clients`; $n++ ) {
			setAttr ( $fractionNodes[$n] +".input2X" ) ($clientLengths[$n]/$totalLength*$parityFactor);

			//now connect the inital coords to the plus node - then connect the
			connectAttr -f ( $finalLength +".outputR" ) ( $fractionNodes[$n] +".input1X" );

			//then connect the result of the plus node to the t(axis) pos of the limb joints
			if( `getAttr -l ( $clients[$n] +".t"+ $axis )` ) setAttr -l 0 ( $clients[$n] +".t"+ $axis );
			connectAttr -f ( $fractionNodes[$n] +".outputX" ) ( $clients[$n] +".t"+ $axis );
			}


	//------
	//now if we have only 3 clients, that means we have a simple limb structure
	//in which case, lets build an elbow pos network
	//------
		string $elbowPos = "";
		if( `size $clients` == 3 && $elbowpos ) {
			float $default = $clientLengths[1]/$totalLength*$parityFactor;
			int $isNeg = $default < 0? 1: 0;

			$default = abs($default);
			addAttr -ln elbowPos -at double  -min 0 -max 1 -dv $default $control;
			setAttr -k 1 ( $control +".elbowPos" );

			$elbowPos = `shadingNode -asUtility reverse`;
			$elbowPos = `rename $elbowPos ( $clients[1] +"_elbowPos")`;
			$allNodes[`size $allNodes`] = $elbowPos;
			if( $isNeg ) {
				string $mult = `shadingNode -asUtility multiplyDivide`;
				setAttr ( $mult +".input2" ) -1 -1 -1;
				connectAttr -f ( $control +".elbowPos" ) ( $elbowPos +".inputX" );
				connectAttr -f ( $control +".elbowPos" ) ( $mult +".input1X" );
				connectAttr -f ( $elbowPos +".outputX" ) ( $mult +".input1Y" );
				connectAttr -f ( $mult +".outputY" ) ( $fractionNodes[2] +".input2X" );
				connectAttr -f ( $mult +".outputX" ) ( $fractionNodes[1] +".input2X" );
				$allNodes[`size $allNodes`] = $mult;
				}
			else {
				connectAttr -f ( $control +".elbowPos" ) ( $elbowPos +".inputX" );
				connectAttr -f ( $elbowPos +".outputX" ) ( $fractionNodes[2] +".input2X" );
				connectAttr -f ( $control +".elbowPos" ) ( $fractionNodes[1] +".input2X" );
				}
			}


	//------
	//now add all variable length nodes to the allNodes array
	//------
		$allNodes = `zooAddArray_str $allNodes $fractionNodes`;


	//------
	//finally, if the user wants the nodes created by this proc registered with a primitive, and then cleanup
	//------
		if( $register ) if( `objExists $primitive` ) zooCSTPrimOutputs $allNodes $primitive;
		if( `objExists $parts` ) parent $loc_a $parts;
		int $idxA = `zooGetIdxOfElement_str $allNodes $loc_a`;
		int $idxB = `zooGetIdxOfElement_str $allNodes $loc_b`;
		$allNodes[$idxA] = `rename $loc_a ( $ikHandle +"_lockthis_extension_start" )`;
		$allNodes[$idxB] = `rename $loc_b ( $ikHandle +"_lockthis_extension_end" )`;

	return $allNodes;
	}


/*
creates stretch attribs on the $control object, and makes all joints controlled by the ikHandle stretchy
-------

$control - the character prefix used to identify the character
$parity - which side is the arm on?  l (left) or r (right)
$ikHandle - the bicep, upper arm, or humerous
$optionStr - standard option string - see technical docs for info on option strings

option flags
-------
-axis [string]			the stretch axis used by the joints in the limb.  default: x
-scale [float]			scale factor to apply to the control (purely a visual thing - scale is frozen).  default: 1
-startObj [string]	the beginning of the measure node is attached to the start object.  if its not specified, then the script assumes the start object is the start of the ik chain (usally the case)
-endObj [string]		this is the object the end of the measure is attached to - by default its the
-invert [int]				use this flag if the script inverts the limb when adding stretch
-parts [string]		the parts node is simply an object that miscellanous dag nodes are parented under - if not specified, miscellanous objects are simply left in worldspace

It is reccommended that this proc has a "root" control, a "chest" control and a "head" control already
built (and branded with these names) as it uses them as dynamic parents for the actual arm control.

For example:
zooCSTMakeStretchy primoBoy_arm_ctrl_L l primoBoy_arm_ctrl_L "-axis x -scale 0.5";
*/


//returns the real length of the limb that an ik handle controls
global proc float[] zooCSTGetIKLength( string $ikHandle ) {
	string $clients[] = `ikHandle -q -jl $ikHandle`;
	float $lengthList[] = {};
	float $length = 0;

	$clients[( `size $clients` )] = `ikHandle -q -ee $ikHandle`;
	for( $n=0; $n<`size $clients`-1; $n++ ) {
		float $posA[] = `xform -q -ws -rp $clients[$n]`;
		float $posB[] = `xform -q -ws -rp $clients[( $n+1 )]`;
		float $sqd = `pow ($posA[0]-$posB[0]) 2` + `pow ($posA[1]-$posB[1]) 2` + `pow ($posA[2]-$posB[2]) 2`;

		$lengthList[( `size $lengthList` )] = `sqrt $sqd`;
		$length += `sqrt $sqd`;
		}

	return $lengthList;
	}


//zoo.end
//macaroniKazoo ©2007